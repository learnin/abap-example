CLASS zcl_string_util DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    "! val ã®æ–‡å­—æ•°ï¼ˆã‚³ãƒ¼ãƒ‰ãƒã‚¤ãƒ³ãƒˆæ•°ï¼‰ã‚’è¿”ã™ï¼ˆã‚µãƒ­ã‚²ãƒ¼ãƒˆãƒšã‚¢æ–‡å­—ã‚‚1æ–‡å­—ã¨æ•°ãˆã‚‹ï¼‰
    "! @parameter val | å¯¾è±¡æ–‡å­—åˆ—
    "! @parameter result | æ–‡å­—æ•°
    CLASS-METHODS strlen_cp
      IMPORTING val           TYPE string
      RETURNING VALUE(result) TYPE i.

    "! val ã® off ä½ç½®ã‹ã‚‰ len æ–‡å­—æ–‡ã®éƒ¨åˆ†æ–‡å­—åˆ—ã‚’è¿”ã™
    "! ä¾‹. substring_cp( val = `ðŸ£ð ®·é‡Žå®¶` off = 1 len = 2 ) "ð ®·é‡Ž
    "! @parameter val | å¯¾è±¡æ–‡å­—åˆ—
    "! @parameter off | éƒ¨åˆ†æ–‡å­—åˆ—ã®é–‹å§‹ä½ç½®ï¼ˆ0å§‹ã¾ã‚Šï¼‰
    "! @parameter len | å¯¾è±¡æ–‡å­—åˆ—ã®æ–‡å­—æ•°
    "! @parameter result | éƒ¨åˆ†æ–‡å­—åˆ—
    CLASS-METHODS substring_cp
      IMPORTING
                val           TYPE string
                off           TYPE i
                len           TYPE i
      RETURNING VALUE(result) TYPE string.

  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-METHODS to_utf32_xstring
      IMPORTING val           TYPE string
      RETURNING VALUE(result) TYPE xstring.

    CLASS-METHODS from_utf32_xstring
      IMPORTING val           TYPE xstring
      RETURNING VALUE(result) TYPE string.
ENDCLASS.

CLASS zcl_string_util IMPLEMENTATION.
  METHOD to_utf32_xstring.
    result = cl_abap_conv_codepage=>create_out( codepage = 'UTF-32BE' )->convert( val ).
  ENDMETHOD.

  METHOD from_utf32_xstring.
    result = cl_abap_conv_codepage=>create_in( codepage = 'UTF-32BE' )->convert( val ).
  ENDMETHOD.

  METHOD strlen_cp.
    result = xstrlen( to_utf32_xstring( val ) ) / 4.
  ENDMETHOD.

  METHOD substring_cp.
    DATA(target_xstr) = to_utf32_xstring( val ).

    FINAL(offset) = off * 4.
    FINAL(length) = len * 4.
    target_xstr = target_xstr+offset(length).

    result = from_utf32_xstring( target_xstr ).
  ENDMETHOD.

ENDCLASS.

